plugins {
    id 'java'
    id "com.palantir.docker" version "0.20.1"
    id "com.palantir.git-version" version "0.12.0-rc2"
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "com.moowork.gulp" version "1.2.0"
}

group 'com.me4502'
version '1.0-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    // Util
    compile group: 'com.google.guava', name: 'guava', version: '26.0-jre'
    implementation 'com.google.code.gson:gson:2.8.5'

    // Web
    compile "com.sparkjava:spark-core:2.7.2"

    // API Services

    // Configuration
    compile 'org.spongepowered:configurate-hocon:3.6'

    // Logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.me4502.emojify.Emojify'
    }
}

shadowJar {
    zip64 = true
}

node {
    version = '6.11.5'
    download = true
    nodeModulesDir = file("client")
}

gulp {
    workDir = file("client")
}

docker {
    name 'me4502/emojify:' + versionDetails().gitHash
    dockerfile file('Dockerfile')
    files tasks.shadowJar.outputs
}

yarn.dependsOn(yarnSetup)
installGulp.dependsOn(yarn)
tasks.findByName('gulp_compile').dependsOn 'installGulp'
jar.dependsOn 'gulp_compile'
shadowJar.dependsOn(jar)
build.dependsOn(shadowJar)
docker.dependsOn(build)
